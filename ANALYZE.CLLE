PGM PARM(&P_TABLE &P_QUESTION)

DCL VAR(&P_TABLE)   TYPE(*CHAR) LEN(50)
DCL VAR(&P_QUESTION) TYPE(*CHAR) LEN(200)
DCL VAR(&OUTPUT)    TYPE(*CHAR) LEN(256)
DCL VAR(&CMD)       TYPE(*CHAR) LEN(2048)
DCL VAR(&JOBNBR)    TYPE(*CHAR) LEN(6)
DCL VAR(&TIMESTAMP) TYPE(*CHAR) LEN(20)
DCL VAR(&T)         TYPE(*CHAR) LEN(50)
DCL VAR(&Q)         TYPE(*CHAR) LEN(200)

RTVJOBA NBR(&JOBNBR)
RTVSYSVAL SYSVAL(QDATETIME) RTNVAR(&TIMESTAMP)

/* 出力ファイルはJOBIDとタイムスタンプをつけ衝突しないようにする */
CHGVAR VAR(&OUTPUT) VALUE('/tmp/gemini_' *TCAT &JOBNBR +
  *TCAT '_' *TCAT %SST(&TIMESTAMP 1 14) *TCAT '.txt')

CHGVAR VAR(&T) VALUE(%TRIM(&P_TABLE))
CHGVAR VAR(&Q) VALUE(%TRIM(&P_QUESTION))

/* 分析プログラムを実行 */
CHGVAR VAR(&CMD) VALUE('/home/blog/gemini/run_analyze.sh "' *TCAT +
  &T *TCAT '" "' *TCAT +
  &Q *TCAT '" "' *TCAT +
  &OUTPUT *TCAT '"')

QSH CMD(&CMD)

/* 出力データ一時格納用のソース物理ファイル作成 */
DLTF FILE(QTEMP/GEMINISRC)
MONMSG MSGID(CPF2105)
CRTSRCPF FILE(QTEMP/GEMINISRC) RCDLEN(2048) CCSID(1208)

/* IFSからソースメンバーにコピー */
CPYFRMSTMF FROMSTMF(&OUTPUT) +
  TOMBR('/QSYS.LIB/QTEMP.LIB/GEMINISRC.FILE/RESULT.MBR') +
  MBROPT(*REPLACE) STMFCCSID(1208)

/* 結果テーブル作成 */
DLTF FILE(QTEMP/GEMINI)
MONMSG MSGID(CPF2105)
RUNSQL SQL('CREATE TABLE QTEMP.GEMINI (SEQ INT, +
  RESULT VARCHAR(2048) CCSID 1208)') COMMIT(*NONE)

/* ソースから読み込み */
RUNSQL SQL('INSERT INTO QTEMP.GEMINI SELECT SRCSEQ, SRCDTA FROM +
  QTEMP.GEMINISRC ORDER BY SRCSEQ') COMMIT(*NONE)

/* 一時ファイル削除 */
RMVLNK OBJLNK(&OUTPUT)
MONMSG MSGID(CPFA0A9)

SNDPGMMSG MSG('完了: SELECT * FROM QTEMP.GEMINI')

ENDPGM
